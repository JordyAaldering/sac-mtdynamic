use Array: all;
use CommandLine: all;
use StdIO: all;

inline
float[m,n] stabilise(float[m,n] x)
{
    return { [i] -> x[i] - maxval(x[i])
           | [i] < [m] };
}

inline
float[d:shp] exp(float[d:shp] x)
{
    return { iv -> Math::exp(x[iv])
           | iv < shp };
}

inline
float[m,n] scale(float[m,n] x)
{
    return { [i] -> x[i] / sum(x)
           | [i] < [m] };
}

inline
float[m,n] matmulT(float[m,k] a, float[n,k] bT)
{
    return { [i,j] -> sum(a[i] * bT[j])
           | [i,j] < [m,n] };
}

noinline
float[Nd,d,d] FlashAttention(float[Nd,d,d] Q, float[N,d] K, float[N,d] V)
{
    MTClock::touch();

    Vt = transpose(V);
    return { [i] -> matmulT(scale(exp(stabilise(matmulT(Q[i], K)))), Vt)
           | [i] < [Nd] };
}

int main()
{
    iter = String::toi(argv(argc() - 3));
    d = String::toi(argv(argc() - 2));
    n = String::toi(argv(argc() - 1));

    Q = { [i, j, k] -> tof(1) / (tof(1) + tof(i)) | [i, j, k] < [n / d, d, d] };
    K = { [i, j]    -> tof(1)                     | [i, j] < [n, d] };
    V = { [i, j]    -> tof(1)                     | [i, j] < [n, d] };

    for (i = 0; i < iter; i += 1) {
        sec, nsec, rapl = Bench::start();
        Q = FlashAttention(Q, K, V);
        Bench::stop(sec, nsec, rapl);
    }

    return toi(Q[0,0,0]);
}
